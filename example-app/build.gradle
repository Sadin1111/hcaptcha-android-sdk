plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def intProp(name, fallback) {
    return project.hasProperty(name) ? Integer.parseInt(project.getProperty(name)) : fallback
}

def prop(name, fallback) {
    return project.hasProperty(name) ? project.getProperty(name) : fallback
}

android {
    compileSdk intProp("exampleCompileSdkVersion", 34)
    namespace 'com.hcaptcha.example'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion intProp("exampleTargetSdkVersion", 34)
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    compileOptions {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
        }
    }

    lint {
        disable 'UsingOnClickInXml'
    }

    buildFeatures { // Enables Jetpack Compose for this module
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_version"
    }
}

dependencies {
    //noinspection GradleDependency
    implementation "androidx.appcompat:appcompat:${prop('exampleAppcompatVersion', '1.3.1')}"
    implementation "com.google.android.flexbox:flexbox:3.0.0"
    implementation project(path: ':sdk')

    implementation 'androidx.compose.ui:ui:1.6.1'
    implementation 'androidx.compose.material3:material3:1.2.0'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation project(path: ':compose')
    implementation 'androidx.compose.foundation:foundation-layout-android:1.6.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
